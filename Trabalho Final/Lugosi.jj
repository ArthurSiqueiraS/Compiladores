PARSER_BEGIN(Lugosi)
import java.io.*;
import java.util.ArrayList;

class P {
  private static int tabs = 0;
  private static Boolean indent = true;

  public static void tab() { 
    tabs++; 
  }

  public static void untab() { 
    if(tabs > 0)
      tabs--; 
  }

  public static void print(String s) {
    if(indent) {
      for(int i = 0; i < tabs; ++i) {
        System.out.print("    ");
      }
    }
    System.out.print(s);

    indent = false;
  }

  public static void println(String s) {
    print(s + "\n");

    indent = true;
  }
}

class ArvoreLugosi {
  Main main;
  ArrayList<Func> funcs;  

  public ArvoreLugosi(Main main, ArrayList<Func> funcs) { 
    this.main = main; 
    this.funcs = funcs;
  }

  public void pprint() {
    main.pprint();
    for(Func func: funcs) {
      P.println("");
      func.pprint();
    }
  }
}

class Main {
  ArrayList<VarDecl> varDecls;
  ArrayList<Comando> comandos;

  public Main(ArrayList<VarDecl> varDecls, ArrayList<Comando> comandos) { 
    this.varDecls = varDecls; 
    this.comandos = comandos; 
  }

  public void pprint() {
    P.println("main {");
      P.tab();
      for(VarDecl varDecl: varDecls) {
        Boolean first = varDecls.indexOf(varDecl) == 0, 
          last = varDecls.indexOf(varDecl) == varDecls.size() - 1;
        varDecl.pprint(first, last);
      }

      for(Comando comando: comandos) {
        comando.pprint();
      }
      P.untab();
    P.println("}");
  }
}

class VarDecl {
  Tipo tipo;
  String var;

  public VarDecl(Tipo tipo, String var) {
    this.tipo = tipo;
    this.var = var;
  }

  public void pprint(Boolean first, Boolean last) {
    if(first)
      P.print("var ");
    else
      P.print(", ");
    tipo.pprint();
    P.print(" " + var);
    if(last)
      P.println(";");
}
}

abstract class Tipo {
  public abstract void pprint();
}

class Int extends Tipo {
  public void pprint() {
    P.print("int");
  }
}

class Bool extends Tipo {
  public void pprint() {
    P.print("bool");
  }
}

abstract class Comando {
  public abstract void pprint();
}

class Atribuicao extends Comando {
  String var;
  Exp exp;

  public Atribuicao(String var, Exp exp) {
    this.var = var;
    this.exp = exp;
  }

  public void pprint() {
    P.print(var + " := ");
    exp.pprint();
    P.println(";");
}
}

class ChamadaFuncao extends Comando {
  String nomeFuncao;
  ArrayList<Exp> argumentos;

  public ChamadaFuncao(String nomeFuncao, ArrayList<Exp> argumentos) {
    this.nomeFuncao = nomeFuncao;
    this.argumentos = argumentos;
  }

  public void pprint() {
    P.print(nomeFuncao + "(");
    for(Exp arg : argumentos) {
      if(!(argumentos.indexOf(arg) == 0))
        P.print(", ");
      arg.pprint();
}
    P.println(");");
  }
}

class Condicional extends Comando {
  Exp cond;
  ArrayList<Comando> then;

  public Condicional(Exp cond, ArrayList<Comando> then) {
    this.cond = cond;
    this.then = then;
  }

  public void pprint() {
    if (this instanceof DoWhile)
      P.println("do {");
    else {
      if (this instanceof If) 
        P.print("if");
      if (this instanceof While) 
        P.print("while");
      P.print("(");
      cond.pprint();
      P.println(") {");
}
      P.tab();
      for(Comando comando: then) {
        comando.pprint();
}
      P.untab();
    if(this instanceof DoWhile) {
      P.print("} while (");
      cond.pprint();
      P.println(");");
    } 
    else 
      P.println("};");
  }
}

class If extends Condicional {
  public If(Exp cond, ArrayList<Comando> then) {
    super(cond, then);
  }
}

class While extends Condicional {
  public While(Exp cond, ArrayList<Comando> then) {
    super(cond, then);
  }
}

class DoWhile extends Condicional {
  public DoWhile(Exp cond, ArrayList<Comando> then) {
    super(cond, then);
  }
}

class Chamada extends Comando {
  Exp exp;

  public Chamada(Exp exp) {
    this.exp = exp;
  }

  public void pprint() {
    if(this instanceof Return)
      P.print("return ");
    else
      P.print("print (");
    exp.pprint();
    if(this instanceof Print)
      P.print(")");
    P.println(";");
}
}

class Return extends Chamada {
  public Return(Exp exp) {
    super(exp);
  }
}

class Print extends Chamada {
  public Print(Exp exp) {
    super(exp);
  }
}

class Func {
  Tipo tipo;
  String nome;
  ArrayList<Arg> argumentos;
  ArrayList<VarDecl> varDecls;
  ArrayList<Comando> comandos;

  public Func(Tipo tipo, String nome, ArrayList<Arg> argumentos, ArrayList<VarDecl> varDecls, ArrayList<Comando> comandos) {
    this.tipo = tipo;
    this.nome = nome;
    this.argumentos = argumentos;
    this.varDecls = varDecls;
    this.comandos = comandos;
  }

  public void pprint() {
    tipo.pprint();
    P.print(" " + nome + "(");
    for(Arg arg : argumentos) {
      arg.pprint(argumentos.indexOf(arg) == 0);
    }
    P.println(") {");
      P.tab();
      for(VarDecl varDecl : varDecls) {
        Boolean first = varDecls.indexOf(varDecl) == 0, last = varDecls.indexOf(varDecl) == varDecls.size() - 1;
        varDecl.pprint(first, last);
      }
      for(Comando comando : comandos) {
        comando.pprint();
      }
      P.untab();
    P.println("}");
  }
}

abstract class Exp {
  public abstract void pprint();
}

class OperadorInfixo extends Exp {
  Exp arg1, arg2;
  String op;

  public OperadorInfixo(Exp arg1, Exp arg2, String op) {
    this.arg1 = arg1;
    this.arg2 = arg2;
    this.op = op;
  }

  public void pprint() {
    P.print("(");
    arg1.pprint();
    P.print(" " + op + " ");
    arg2.pprint();
    P.print(")");
  }
}

abstract class Fator extends Exp {}

class TokenId extends Fator {
  String token;

  public TokenId(String token) {
    this.token = token;
  }

  public void pprint() {
    P.print(token);
  }
}

class FuncFator extends Fator {
  String func;
  ArrayList<Exp> argumentos;

  public FuncFator(String func, ArrayList<Exp> argumentos) {
    this.func = func;
    this.argumentos = argumentos;
  }

  public void pprint() {
    P.print(func + "(");
    for(Exp arg : argumentos) {
      if(!(argumentos.indexOf(arg) == 0))
        P.print(", ");
      arg.pprint();
    }
    P.print(")");
  }
}

class TokenNum extends Fator {
  String num;

  public TokenNum(String num) {
    this.num = num;
  }

  public void pprint() {
    P.print(num);
  }
}

class True extends Fator {
  public void pprint() {
    P.print("true");
  }
}

class False extends Fator {
  public void pprint() {
    P.print("false");
  }
} 

class Arg {
  Tipo tipo;
  TokenId tokenId;

  public Arg(Tipo tipo, TokenId tokenId) {
    this.tipo = tipo;
    this.tokenId = tokenId;
  }

  public void pprint(Boolean first) {
    if(!first)
      P.print(", ");
    tipo.pprint();
    P.print(" ");
    tokenId.pprint();
  }
}

public class Lugosi {

  public static void main(String args[]) throws ParseException,IOException {
    
    Lugosi parser = new Lugosi(new FileInputStream(args[0]));
    ArvoreLugosi arvore = parser.Lugosi();
    arvore.pprint();
  }
}

PARSER_END(Lugosi)

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
}

TOKEN :
{
  <MAIN: "main"> | <VAR: "var">
| <ATRIB: ":="> | <IF: "if"> | <WHILE: "while"> | <DO: "do">
| <RETURN: "return"> | <PRINT: "print"> | <FUNCTION: "function">
| <ACHAVES: "{"> | <FCHAVES: "}"> | <APARNTS: "("> | <FPARNTS: ")"> 
| <VIRG: ","> | <PTVIRG: ";">
}

TOKEN :
{
  <NUM : (["0"-"9"])+("."(["0"-"9"])+)?("E"("+"|"-")?(["0"-"9"])+)?>
| <BOOL: "true" | "false"> 
| <TIPO: "int" | "bool"> 
| <OP: "+" | "-" | "*" | "/" | "&&" | "||" | "<" | ">" | "==">
| <ID: ["a"-"z","A"-"Z"](("_")?["a"-"z","A"-"Z","0"-"9"])*>
}

ArvoreLugosi Lugosi () :
{
  Main main;
  ArrayList<Func> funcs = new ArrayList<Func>();
}
{ 
  main = Main()
  (
    Func(funcs)
  )?
  <EOF>

  { return new ArvoreLugosi(main, funcs); }
}	

Main Main ():
{
  ArrayList<VarDecl> varDecls;
  ArrayList<Comando> comandos;
}
{
  <MAIN>
  <ACHAVES> 
  varDecls = VarDecl()
  comandos = SeqComandos()
  <FCHAVES>
  
  { return new Main(varDecls, comandos); }
}

ArrayList<VarDecl> VarDecl() :
{
  ArrayList<VarDecl> varDecls = new ArrayList<VarDecl>();
  Tipo tipo;
  Token var, tokenTipo;
  Int tipoInt = new Int();
  Bool tipoBool = new Bool();
}
{
  (
    <VAR> 
    tokenTipo = <TIPO>
    var = <ID>
    <PTVIRG> 
    {
      tipo = tokenTipo.toString().equals("int") ? tipoInt : tipoBool;
      varDecls.add(new VarDecl(tipo, var.toString()));
    }
  )*

  { return varDecls; }
}

ArrayList<Comando> SeqComandos() :
{
  ArrayList<Comando> comandos = new ArrayList<Comando>();
  Comando comando;
}
{
  (
    comando = ComanCondBlock()
    { comandos.add(comando); }
  )*

  { return comandos; }
}

Comando ComanCondBlock() :
{ Comando comando; }
{
  {
    Token nomeFuncao;
    ArrayList<Exp> argumentos;
  }
  (
    nomeFuncao = <ID> 
    comando = Comando_(nomeFuncao.toString())
  )

  { return comando; }
| 
  {
    Exp cond;
    ArrayList<Comando> then;
  }
(
    (
      <IF>
      cond = ExpPar()
      then = CondBlock()
      <PTVIRG>
    )

    { return new If(cond, then); }
  | 
    (
      <WHILE>
      cond = ExpPar()
      <DO>
      then = CondBlock() 
      <PTVIRG>
    )

    { return new While(cond, then); }
  | 
    (
      <DO>
      then = CondBlock()
      <WHILE>
      cond = ExpPar() 
      <PTVIRG>
    )

    { return new DoWhile(cond, then); }
)
|  
   { Exp exp; }
(
    (
      <RETURN>
      exp = Exp()
      <PTVIRG>
    )

    { return new Return(exp); }
| 
    (
      <PRINT>
      exp = ExpPar()
      <PTVIRG>
    )

    { return new Print(exp); }
)
}

ArrayList<Comando> CondBlock() :
{ ArrayList<Comando> comandos; }
{
  <ACHAVES> 
  comandos = SeqComandos() 
  <FCHAVES> 

  { return comandos; }
}

Exp ExpPar() :
{ Exp exp; }
{
  <APARNTS> 
  exp = Exp() 
  <FPARNTS>

  { return exp; }
}

Comando Comando_(String id) :
{}
{
  { Exp argumento; }
  (
    <ATRIB> 
    argumento = Exp() 
    <PTVIRG>
  )
  { return new Atribuicao(id, argumento); }
| 
  { ArrayList<Exp> argumentos = new ArrayList<Exp>(); }
  (
    <APARNTS> 
    (
      ListaExp(argumentos)
    )? 
    <FPARNTS> 
    <PTVIRG>
  )  
  { return new ChamadaFuncao(id, argumentos); }
}

Exp Exp() :
{}
{
  {
    Exp arg1, arg2;
    Token op;
  }
  (
    <APARNTS> 
    arg1 = Exp() 
    op = <OP> 
    arg2 = Exp() 
    <FPARNTS>
  )

  { return new OperadorInfixo(arg1, arg2, op.toString()); }
| 
  { Exp fator; }
  fator = Fator() 
  { return fator; }
}

Fator Fator() :
{}
{
  {
    Token token;
    String func = null;
    ArrayList<Exp> argumentos = new ArrayList<Exp>();
  }
  (
    token = <ID> 
    (
      { func = token.toString(); }
      <APARNTS> 
      (
        ListaExp(argumentos)
      )? 
      <FPARNTS>
    )?
  )

  { return func == null ? new TokenId(token.toString()) : new FuncFator(token.toString(), argumentos); }
| 
  { Token num; }
  num = <NUM>

  { return new TokenNum(num.toString()); }
| 
  {
    Fator bool;
    Token boolToken;
  }
  boolToken = <BOOL>
  
  { return boolToken.toString().equals("true") ? new True() : new False(); }
}

void ListaExp(ArrayList<Exp> exps) :
{ Exp exp; }
{
  exp = Exp()
  { exps.add(exp); } 
  ListaExp_(exps)
}

void ListaExp_(ArrayList<Exp> exps) :
{ Exp exp; }
{
  (
    <VIRG> 
    exp = Exp()
    { exps.add(exp); }
  )*
}

void Func(ArrayList<Func> funcs) :
{
  Tipo tipo;
  Token nome, tokenTipo;
  ArrayList<Arg> argumentos = new ArrayList<Arg>();
  ArrayList<VarDecl> varDecls;
  ArrayList<Comando> comandos;
  Int tipoInt = new Int();
  Bool tipoBool = new Bool();
}
{
  (
    <FUNCTION> 
    tokenTipo = <TIPO> 
    nome = <ID> 
    <APARNTS> 
    (
      ListaArg(argumentos)
    )? 
    <FPARNTS> 
    <ACHAVES> 
    varDecls = VarDecl() 
    comandos = SeqComandos() 
    <FCHAVES>

    { 
      tipo = tokenTipo.toString().equals("int") ? tipoInt : tipoBool;
      funcs.add(new Func(tipo, nome.toString(), argumentos, varDecls, comandos));
    }
  )+ 
}

void ListaArg(ArrayList<Arg> argumentos) :
{
  Tipo tipo;
  Token token, tokenTipo;
  Int tipoInt = new Int();
  Bool tipoBool = new Bool();
}
{
  tokenTipo = <TIPO>
  token = <ID>
  {
    tipo = tokenTipo.equals("int") ? tipoInt : tipoBool;
    argumentos.add(new Arg(tipo, new TokenId(token.toString())));
  } 
  (
    ListaArg_(argumentos, tipoInt, tipoBool)
  )+
}

void ListaArg_(ArrayList<Arg> argumentos, Int tipoInt, Bool tipoBool) :
{
  Tipo tipo;
  Token token, tokenTipo;
}
{
  <VIRG> 
  tokenTipo = <TIPO>
  token = <ID>
  {
    tipo = tokenTipo.equals("int") ? tipoInt : tipoBool;
    argumentos.add(new Arg(tipo, new TokenId(token.toString())));
  }
}